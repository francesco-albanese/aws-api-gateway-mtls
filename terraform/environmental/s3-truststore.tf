# S3 bucket for mTLS truststore (per-environment)
# Stores the certificate chain bundle (IntermediateCA.pem + RootCA.pem) for API Gateway mTLS

locals {
  truststore_pem_location = "${path.root}/../../ca_operations/output/${var.account_name}/truststore/truststore.pem"
}

resource "aws_s3_bucket" "mtls_truststore" {
  bucket_prefix = "${var.account_name}-mtls-truststore-"

  tags = {
    Name    = "${var.account_name}-mtls-truststore"
    Purpose = "API Gateway mTLS certificate chain"
  }
}

resource "aws_s3_bucket_versioning" "mtls_truststore" {
  bucket = aws_s3_bucket.mtls_truststore.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "mtls_truststore" {
  bucket = aws_s3_bucket.mtls_truststore.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "mtls_truststore" {
  bucket = aws_s3_bucket.mtls_truststore.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# Upload truststore bundle from local file (generated by bootstrap script)
resource "aws_s3_object" "truststore_bundle" {
  bucket       = aws_s3_bucket.mtls_truststore.id
  key          = "truststore.pem"
  source       = local.truststore_pem_location
  source_hash  = filemd5(local.truststore_pem_location)
  content_type = "application/x-pem-file"

  tags = {
    Name = "mTLS truststore bundle"
  }
}
